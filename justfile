# Eth P2P Notebooks - Pipeline Commands

# Default recipe
default:
    @just --list

# ============================================
# Development
# ============================================

# Start Astro development server
dev:
    cd site && pnpm dev

# Preview production build
preview:
    cd site && pnpm preview

# Install all dependencies
install:
    uv sync
    cd site && pnpm install
    git config core.hooksPath .githooks

# ============================================
# Data Pipeline
# ============================================

# Fetch data: all (default) or specific date (YYYY-MM-DD)
# Fetch data: all (default) or specific date (YYYY-MM-DD). Support force="true" to force re-fetch.
fetch target="all" force="false":
    uv run python scripts/fetch_data.py --output-dir notebooks/data \
        {{ if target == "all" { "--sync" } else { "--date " + target } }} \
        {{ if force == "true" { "--force" } else { "" } }}

# Check for stale data without fetching
check-stale:
    uv run python scripts/pipeline.py check-stale

# Show resolved date range from config
show-dates:
    uv run python scripts/pipeline.py resolve-dates

# Show current query hashes
show-hashes:
    uv run python scripts/pipeline.py query-hashes

# ============================================
# Data Pipeline (PQ Devnet)
# ============================================

# Detect devnet iterations from Prometheus
detect-devnets days="7":
    uv run python scripts/pqdevnet/detect_devnets.py --days {{ days }}

# Fetch data for a devnet (e.g., devnet-001) or "all" devnets
fetch-devnet devnet query="":
    uv run python scripts/pqdevnet/fetch_data_prometheus.py --output-dir notebooks/data \
        --devnet {{ devnet }} \
        {{ if query != "" { "--query " + query } else { "" } }}

# List detected devnets
list-devnets:
    uv run python scripts/pqdevnet/fetch_data_prometheus.py --list-devnets

# List available PQ Devnet metrics from Prometheus
list-prometheus-metrics:
    uv run python scripts/pqdevnet/fetch_data_prometheus.py --list-metrics

# List available Prometheus queries
list-prometheus-queries:
    uv run python scripts/pqdevnet/fetch_data_prometheus.py --list-queries

# Render PQ Devnet notebooks for a devnet (e.g., devnet-005) or "all"
render-devnet devnet:
    uv run python scripts/pqdevnet/render_notebooks.py --devnet {{ devnet }}

# Full PQ Devnet pipeline: detect + fetch + render for a devnet
sync-devnet devnet:
    just detect-devnets
    just fetch-devnet {{ devnet }}
    just render-devnet {{ devnet }}

# ============================================
# Notebook Rendering
# ============================================

# Render notebooks: all (default), "latest", or specific date (YYYY-MM-DD). Support force="true" to force re-render.
render target="all" force="false":
    uv run python scripts/render_notebooks.py --output-dir site/rendered \
        {{ if target == "all" { "" } \
           else if target == "latest" { "--latest-only" } \
           else { "--date " + target } }} \
        {{ if force == "true" { "--force" } else { "" } }}

# ============================================
# Build & Deploy
# ============================================

# Build Astro site
build:
    cd site && pnpm build

# Copy parquet files to dist for R2 publishing (only rendered dates)
copy-data:
    uv run python scripts/copy_data_to_dist.py

# Render all + build Astro + copy data for publishing
publish: render build copy-data

# ============================================
# CI / Full Pipeline
# ============================================

# Full sync: fetch + render + build
sync: fetch render build

# CI: Check data staleness (exit 1 if stale)
check-stale-ci:
    uv run python scripts/fetch_data.py --output-dir notebooks/data --check-only

# ============================================
# Utilities
# ============================================

# Warn about stale data but don't fail
check-stale-warn:
    uv run python scripts/pipeline.py check-stale || echo "Warning: Some data may be stale"

# Type check the Astro site
typecheck:
    cd site && pnpm typecheck

# Clean build artifacts
clean:
    rm -rf site/dist site/.astro site/rendered

# Clean all (including node_modules and venv)
clean-all: clean
    rm -rf site/node_modules .venv
